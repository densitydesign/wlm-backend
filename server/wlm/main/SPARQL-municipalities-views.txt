SELECT DISTINCT

  ?mon ?monLabel
  #?locationLabel
  
  ?article

  #(group_concat(DISTINCT ?parent; separator=";") as ?parent_n)
  (group_concat(DISTINCT ?geo; separator=";") as ?geo_n)
  (group_concat(DISTINCT ?place; separator=";") as ?place_n)
  (group_concat(DISTINCT ?place_geo; separator=";") as ?place_geo_n)
  (group_concat(DISTINCT ?wlm; separator=";") as ?wlm_n)
  (group_concat(DISTINCT ?start; separator=";") as ?start_n)
  (group_concat(DISTINCT ?end; separator=";") as ?end_n)
  (group_concat(DISTINCT ?approvedBy; separator=";") as ?approvedBy_n)
  (group_concat(DISTINCT ?relevantImage; separator=";") as ?relevantImage_n)
  (group_concat(DISTINCT ?commonsCat; separator=";") as ?commons_n)

WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language "it, [AUTO_LANGUAGE]". }
  
  # seleziona i monumenti partecipanti a WLM in Italia
  ?mon wdt:P131* wd:Q38;
        p:P2186 ?wlms.
        
  # restringe ai soli comuni
  {?mon wdt:P31 wd:Q747074.} UNION {?mon wdt:P31 wd:Q954172.}
  
  ?wlms ps:P2186 ?wlm.
  
  OPTIONAL { ?wlms pq:P580 ?start . }
  OPTIONAL { ?wlms pq:P582 ?end . }
  OPTIONAL { ?wlms pq:P790 ?approvedBy. }
  
  OPTIONAL { ?mon wdt:P625 ?geo . }
  OPTIONAL {
    ?mon wdt:P131 ?place .
    OPTIONAL {
      ?place wdt:P625 ?place_geo .
    }
  }
  
  #OPTIONAL { ?mon wdt:P361 ?parent }
  OPTIONAL { ?mon wdt:P18 ?relevantImage . }
  OPTIONAL { ?mon wdt:P373 ?commonsCat . }

  #OPTIONAL { ?mon wdt:P276 ?location . }
  OPTIONAL {
    ?article schema:about ?mon .
    FILTER REGEX(STR(?article), ".it.wikipedia.org/wiki/") .
  }

}

GROUP BY ?mon ?monLabel ?article
ORDER BY ?monLabel
